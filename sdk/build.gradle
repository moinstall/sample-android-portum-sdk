apply plugin: 'com.android.library'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.1"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "AD_SERVER_SCHEME", "\"http\""
            buildConfigField "String", "AD_SERVER_HOST", "\"adserver.vradx.com\""
        }
        debug {
            buildConfigField "String", "AD_SERVER_SCHEME", "\"http\""
            buildConfigField "String", "AD_SERVER_HOST", "\"adserver.stg.vradx.com\""
        }
    }

    lintOptions {
        warning 'InvalidPackage' // https://github.com/square/okio/issues/58
    }
}

task javadoc(type: Javadoc) {
    title = rootProject.name
    destinationDir = file("$buildDir/outputs/docs/javadoc/")

    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    
    options.links("http://docs.oracle.com/javase/7/docs/api/");
    options.links("https://developer.android.com/reference/");

    exclude '**/BuildConfig.java'
    exclude '**/R.java'


    failOnError false  
}

afterEvaluate {
    javadoc.classpath += files(android.libraryVariants.collect { variant ->
        variant.javaCompile.classpath.files
    })
}

dependencies {
    compile 'com.android.support:appcompat-v7:25.1.0'

    compile 'com.squareup.okhttp3:okhttp:3.5.0'
    compile 'com.squareup.moshi:moshi:1.3.1'

    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
}
